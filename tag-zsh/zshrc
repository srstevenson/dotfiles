# ~/.zshrc

print-short-pwd() {
    setopt local_options extended_glob
    local directory="${PWD/#$HOME/~}"
    if [[ "$directory" == (#m)[/~] ]]; then
        print "$MATCH"
    else
        print "${${${${(@j:/:M)${(@s:/:)directory}##.#?}:h}%/}//\%/%%}/${${directory:t}//\%/%%}"
    fi
}

autoload -Uz add-zsh-hook
add-zsh-hook precmd () { print -Pn "\e]0;%m: $(print-short-pwd)\a" }
add-zsh-hook preexec () { print -Pn "\e]0;%m: $2\a" }

+vi-git-stash() {
    if [[ -s "${hook_com[base]}/.git/refs/stash" ]]; then
        hook_com[misc]+="%F{magenta}%B\$%b%f"
    fi
}

+vi-git-untracked() {
    if [[ "$(git rev-parse --is-inside-work-tree 2> /dev/null)" == "true" ]] && \
        git status --porcelain | grep -q "^?? " 2> /dev/null; then
        hook_com[unstaged]+="%F{red}%B?%b%f"
    fi
}

autoload -Uz vcs_info
add-zsh-hook precmd () { vcs_info }

zstyle ":vcs_info:*" enable git hg
zstyle ":vcs_info:*" check-for-changes true
zstyle ":vcs_info:hg*" get-revision true
zstyle ":vcs_info:hg*" hgrevformat ""
zstyle ":vcs_info:*" formats "(%b%c%u%m)"
zstyle ":vcs_info:*" actionformats "(%b|%a%c%u%m)"
zstyle ":vcs_info:*" unstagedstr "%F{yellow}%B!%b%f"
zstyle ":vcs_info:*" stagedstr "%F{green}%BÂ±%b%f"
zstyle ":vcs_info:git+set-message:*" hooks git-stash git-untracked

setopt PROMPT_SUBST
PROMPT='%F{cyan}%B$(print-short-pwd)%b%f${vcs_info_msg_0_}%# '

bindkey -e

HISTFILE="$HOME/.zsh_history"
SAVEHIST=5000
HISTSIZE=5000
WORDCHARS="${WORDCHARS/\/}"

setopt MENU_COMPLETE

autoload -Uz compinit
compinit

LS_COLORS="di=34:ln=35:so=32:pi=33:ex=31:bd=36;01:cd=33;01:su=31;40;07:sg=36;40;07:tw=32;40;07:ow=33;40;07:"

zstyle ":completion:*" rehash true
zstyle ":completion:*" use-cache on
zstyle ":completion:*" menu select
zstyle ":completion:*" file-sort access
zstyle ":completion:*:default" list-prompt "%F{yellow}%m matches%f"
zstyle ":completion:*:warnings" format "%F{red}No matches%f"
zstyle ":completion:*:*:*:*:descriptions" format "%F{yellow}Matching %d:%f"
zstyle ":completion:*:*:*:*:corrections" format "%F{yellow}Matching %d with %e corrections:%f"
zstyle ":completion:*" group-name ""
zstyle ":completion:*" completer _complete _match _approximate
zstyle ":completion:*" matcher-list "m:{a-zA-Z}={A-Za-z}" "r:|[._-]=* r:|=*" "l:|=* r:|=*"
zstyle ":completion:*:default" list-colors "${(s.:.)LS_COLORS}"

alias la="ls --color=auto -Ahl"
alias ll="ls --color=auto -hl"
alias ls="ls --color=auto -h"

eval "$(direnv hook zsh)"
eval "$(jump shell zsh)"

if [[ -z "$TMUX" && -n "$SSH_TTY" ]]; then
    tmux attach-session || tmux new-session
fi
