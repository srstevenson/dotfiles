# ~/.zshrc

setopt correct
setopt hist_reduce_blanks
setopt interactive_comments
setopt menu_complete
setopt no_clobber

typeset -U fpath
fpath=(~/.zsh/functions $fpath)

autoload -Uz promptinit
promptinit
prompt srs

autoload -Uz compinit
compinit

zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=2
zstyle ':completion:*' select-prompt %SScrolling active: %p%s
(( $+commands[dircolors] )) && eval "$(dircolors -b)"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:descriptions' format '%F{green}Completing %d%f'
zstyle ':completion:*:corrections' format '%F{yellow}Completing %d (errors: %e)%f'
zstyle ':completion:*:warnings' format '%F{red}No matches found%f'

bindkey -e
bindkey "\e[Z" reverse-menu-complete

WORDCHARS=${WORDCHARS:s./.}

HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

typeset -U path
path=(
    ~/.local/bin
    ~/.cabal/bin(N)
    /opt/homebrew/bin(N)
    /opt/texlive/bin/x86_64-*(N)
    $path
)

if [[ ! -d "$TMPDIR" ]]; then
    export TMPDIR=/tmp/$LOGNAME
    mkdir -p -m 700 $TMPDIR
fi

export EDITOR=vim
export GPG_TTY="$(tty)"
export HOMEBREW_NO_GITHUB_API=
export LESS=FiRX
export VISUAL=vim

if ls --color -d . > /dev/null 2>&1; then
    alias ls="ls --color=auto -h"
elif ls -Gd . > /dev/null 2>&1; then
    alias ls="ls -Gh"
fi
alias ll="ls -l"
alias la="ll -A"

alias gad="git add"
alias gap="git add --patch"
alias gbr="git branch"
alias gcb="git checkout -b"
alias gci="git commit --verbose"
alias gco="git checkout"
alias gdc="git diff --cached"
alias gdi="git diff"
alias gll="git log --decorate --graph"
alias gls="git log --decorate --format=gls --graph"
alias gpl="git pull"
alias gps="git push"
alias gst="git status --branch --short"

alias ag="ag --pager less"
alias cdc="cd \$(find ~/code -type d -name .git | sed 's./\.git$..' | pick)"
alias cdd="cd ~/.dotfiles"
alias cdp="cd ~/.dotfiles-private"
alias cp="cp -i"
alias fig="find . | ag"
alias muc="history 1 | awk '{print \$2}' | sort | uniq -c | sort -nr | head -n 20"
alias mv="mv -i"
alias rm="rm -i"
alias tree="tree -C"

[[ -f ~/.zshrc-local ]] && source ~/.zshrc-local
