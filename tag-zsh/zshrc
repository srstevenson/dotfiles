bindkey -e

setopt HIST_IGNORE_DUPS

HISTSIZE=10000
SAVEHIST=10000
WORDCHARS="${WORDCHARS//\//}"

export HOMEBREW_NO_ANALYTICS=1
if [[ -d /opt/homebrew ]]; then
  . <(/opt/homebrew/bin/brew shellenv zsh)
elif [[ -d /home/linuxbrew/.linuxbrew ]]; then
  . <(/home/linuxbrew/.linuxbrew/bin/brew shellenv zsh)
fi

path=(~/dev/local/bin ~/.local/bin ~/.cargo/bin $path)

export EDITOR="hx"
export LESS="FRX"
export RIPGREP_CONFIG_PATH="$HOME/.config/ripgrep/config"

if command -v bitwarden-desktop &>/dev/null || [[ -d /Applications/Bitwarden.app ]]; then
  export SSH_AUTH_SOCK="$HOME/.bitwarden-ssh-agent.sock"
fi

autoload -U compinit
compinit

zstyle ":completion:*:corrections" format "%F{yellow}Matches for %d (%e errors):%f"
zstyle ":completion:*:descriptions" format "%F{green}Matches for %d:%f"
zstyle ":completion:*:messages" format "%F{purple}%d%f"
zstyle ":completion:*:warnings" format "%F{red}No matches for %d.%f"
zstyle ":completion:*" complete-options true
zstyle ":completion:*" completer _extensions _complete _match _approximate
zstyle ":completion:*" group-name ""
zstyle ":completion:*" matcher-list "m:{a-zA-Z}={A-Za-z}" "r:|[._-]=* r:|=*" "l:|=* r:|=*"
zstyle ":completion:*" menu select
zstyle ":completion:*" rehash true
zstyle ":completion:*" use-cache on
zstyle ":completion:*" verbose yes

autoload -U add-zsh-hook

_precmd_title() {
  print -Pn "\e]0;%m: %1~\a"
}

_preexec_title() {
  print -Pn "\e]0;%m: %1~ â€” $2\a"
}

add-zsh-hook precmd _precmd_title
add-zsh-hook preexec _preexec_title

alias la="ls --color -Ahl"
alias ll="ls --color -hl"
alias ls="ls --color -h"

cdd() {
  local dir="$(bfs ~/D*/ -type d | fzy)"
  [[ -d "$dir" ]] && cd "$dir"
}

cdr() {
  local repo="$(find-repos ~/dev | fzy)"
  [[ -d ~/dev/"$repo" ]] && cd ~/dev/"$repo"
}

eval "$(starship init zsh)"
